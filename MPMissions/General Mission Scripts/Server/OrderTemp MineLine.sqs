; args: [unit, si, gi, [wp0, wp1, duration]]

_unit = _this select 0
_si = _this select 1
_gi = _this select 2
_params = _this select 3

; Get Correct Parameters. Need Pay Attention To Coefficients
_wp0 = ((wpCO select _si) select (_params select 0)); _pos0 = [_wp0 select 0, _wp0 select 1]
_wp1 = ((wpCO select _si) select (_params select 1)); _pos1 = [_wp1 select 0, _wp1 select 1]
_endPoints = [_pos0, _pos1]
; Define _endPoints in form of this can make them fix positions.
_duration = 180 * ((_params select 2) + 1)

; Check Whether Unit Fits The Parameters Of The Temporary Order
_vehicle = vehicle _unit
? _unit != _vehicle : exit

_rearmData = _unit call funcGetRearmData
_hasMines = (magMine in (_rearmData select 1))
? !_hasMines : exit

; Check Whether Unit In Another Temporary Order (Except For Set Flight Altitude, Clear Temporary Order, Etc)
? call temporaryCheck : exit

; Initialize The Order Script
_timeStart = time; _timeAbort = _timeStart + _duration; _timeUp = _timeStart; _cycle = 30; _loop = 0
call temporarySet


; Wait For Unit Finish Its Busy-Work
#WaitReady
~2
? time > _timeAbort : 	call temporaryClear; exit
? !call temporaryCheck : exit
? call busyCheck : goto "WaitReady"

; Main Part Of Temporary Order

_timeCheckSupport = time + 30 + random 60
_bCheckDropBomb = true
_second = false
_posMove = []


_groups = groupMatrix select _si
#Update
	_gi = [(group _unit), _groups] call funcGetIndex
	_vehicle = vehicle _unit
	? _unit != driver _vehicle : goto "Sleep"

	; check support
	_mines = "_x == magMine" count magazines _unit
	? time > _timeCheckSupport || _mines == 0 : _timeCheckSupport = time + timeCheckSupport; [_unit, _si, _gi] exec "Server\AI_CheckSupport.sqs"; _cycle = 10; goto "Sleep"

	? (count _posMove) == 0 : _vect = [_wp1, _wp0] call funcVectorSub; _posMove = [_wp0, [_vect, random 1] call funcVectorScale] call funcVectorAdd
	_distToDest = [getPos _unit, _posMove] call funcDistH
	? _distToDest > 10 : goto "Move"
	
	_mines = "_x == magMine" count magazines _unit
	doStop _unit
	_res = [ getPos _unit, siBoth, vMinerAvoid, [] ] call funcGetClosestVehicle
	? (_res select 1) > 8 : _unit fire [weaponPut, magMine, magMine]
	
		_timeUp = time + 4
		@ ( time > _timeUp || !call temporaryCheck )
		? !call temporaryCheck : goto "EOF"
		? !(alive _unit) || time > _timeAbort : goto "EOF"
		
	_minesAfter = "_x == magMine" count magazines _unit
	? _mines == 1 && _minesAfter == 1 : _unit removeMagazine magMine
	_mines = "_x == magMine" count magazines _unit
	_posMove = []
	? _mines == 0 : [_unit, _si, _gi, true] exec "Server\AI_CheckSupport.sqs"; _cycle = 10; goto "Sleep"
	_cycle = 2; goto "Sleep"
	
	#Move
		_distToDest = _distToDest - (_distToDest % 10)
		[_unit, _posMove] call funcMoveAI
		_cycle = [30, 0.1*_distToDest] select (_distToDest < 300)
		goto "Sleep"



; End Of File
#EOF
call temporaryClear
exit


#Sleep
_loop = (time-_timeStart)/_cycle; _loop = 1 + _loop - _loop%1
_timeUp = _loop*_cycle + _timeStart

@ ( time > _timeUp || !call temporaryCheck )
? !call temporaryCheck : goto "EOF"
? !(alive _unit) || time > _timeAbort : goto "EOF"
? call busyCheck : goto "Sleep"
goto "Update"