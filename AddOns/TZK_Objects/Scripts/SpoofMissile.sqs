; args: [target, ammo, whoFired]
_target = _this select 0
? !local _target : goto "EOF"
? "_x=={SpoofSwitch_xj200}" count magazines _target > 0 : goto "EOF"

; this script can be used in TZK CTI only, since it uses some functions defined in CTI missions.
; it's possible to transplant them into addons or other missions but this remain to those editors

? !alive _target: goto "EOF"
? !isengineon _target: goto "EOF"

; Can't spoof if out of SpoofMags
? "_x=={SpoofCount_xj200}" count magazines _target == 0 : goto "EOF"

; Random chance not to spoof
? random 100 < 15: goto "EOF"

; Who's the bad guy?
_enemy = _this select 2

; And what did he fire?
_weaponname = _this select 1

; Get the missile object that was launched at us
_missile = nearestobject [_enemy, _weaponname]

; Wait till the missile is close
_timeAbort = time + 10
@ (_missile distance _target < 400 || _timeAbort < time)
; can't spoof missile fired very close
? _timeAbort < time || _missile distance _target < 250 : goto "EOF"

; Check if Spoof chanse left
? "_x=={SpoofCount_xj200}" count magazines _target == 0 : goto "EOF"

; Mag cost and deflect missile
_target removeMagazine "SpoofCount_xj200"
? !isNull player: if ( (driver _target) in units group player ) then {hint format ["%1 Spoof Chance left", "_x=={SpoofCount_xj200}" count magazines _target]}
_dirchange = 1.57 + 1.57*(speed _target)/200

_index =0
_count = 2*(speed _target)/50

#BendLoop
_vel = velocity _missile
_newvel = [_vel, _dirchange] call funcRandomRotation3D

_missile setVelocity _newvel
_index = _index + 1
~ 0.05 + exp (-(speed _target)/100)

? (_index < _count) : goto "BendLoop"

#EOF
exit