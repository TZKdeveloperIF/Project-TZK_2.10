; args: [Plane]

_plane = _this select 0
[_plane] exec "\TZK_Objects\Scripts\AfterBurnerA10Effect.sqs"; pvAfterBurnerON = _plane; publicVariable "pvAfterBurnerON"
; Set the anim trigger
_plane animate["AB", 1]

; Colour the flame
_plane setobjecttexture[0, "\TZK_Objects\burner.paa"]
_plane setobjecttexture[1, "\TZK_Objects\burner.paa"]

#Loop
?(!isengineon _plane) : goto "Exit"

; Acc. factor
_factor = ((800 - (speed _plane)) / 70000) + 1;

; Change the velocity
_vel = velocity _plane
_plane setvelocity[(_vel select 0) * _factor, (_vel select 1) *_factor, (_vel select 2) *_factor]

; Decrease the fuel
_newfuel = fuel _plane - .0004
?(_newfuel < 0) : goto "Exit"
_plane setfuel _newfuel

~0.1

; See if the ab is still on
?(_plane animationphase "AB" > 0.5) : goto "Loop"

#Exit
; The engine may have gone off as well, so kill the trigger
_plane animate["AB", 0]; pvAfterBurnerOFF = _plane; publicVariable "pvAfterBurnerOFF"

; Put out the lights
_plane setobjecttexture[0, ""]
_plane setobjecttexture[1, ""]

exit
